module analysis/MethodScript

imports
	signatures/-

rules
	
	// Scope initialization.
	init ^ (s) :=
		new s.
	
	// MethodScript program.
	[[ Empty() ^ (s) ]] :=
		true.
	[[ Ms(statements) ^ (scope_global) ]] :=
		
		// Create file scope as child of the global scope.
		new scope_file,
		scope_file ---> scope_global,
		
		distinct/name D(scope_global)/Proc | error $[Duplicate procedure declaration: [NAME]] @NAMES,
		Map1[[ statements ^ (scope_file, scope_global) ]].
	
	// Variable references.
	[[ IVar(varName) ^ (s, scope_global) ]] :=
		IVar{varName} -> s, // varName is a reference in s.
		IVar{varName} |-> d | error $[Cannot find variable: [varName]].
	[[ Var(varName) ^ (s, scope_global) ]] :=
		Var{varName} -> s, // varName is a reference in s.
		Var{varName} |-> d | error $[Cannot find variable: [varName]].
	
	// IVariable parameters.
	// With 1 scope, the param is declared and resolved in the same scope.
	// With 2 scopes, the param is declared in the second scope and its value resolved in the first.
	[[ IVarParam(varName) ^ (s, scope_global) ]] :=
		IVar{varName@s} <- s.
	[[ IVarAssignParam(varName, valExp) ^ (s, scope_global) ]] :=
		IVar{varName@s} <- s,
		[[ valExp ^ (s, scope_global) ]].
	[[ IVarScopeParam(varName) ^ (scope_parent, scope_params, scope_global) ]] :=
		IVar{varName} <- scope_params.
	[[ IVarAssignScopeParam(varName, valExp) ^ (scope_parent, scope_params, scope_global) ]] :=
		IVar{varName} <- scope_params,
		[[ valExp ^ (scope_parent, scope_global) ]].
	
	// Static values.
	[[ IntVal(_) ^ (s, scope_global) ]] :=
		true.
	[[ FloatVal(_) ^ (s, scope_global) ]] :=
		true.
	[[ StringVal(_) ^ (s, scope_global) ]] :=
		true.
	[[ SmartStringVal(_) ^ (s, scope_global) ]] :=
		true.
	[[ True() ^ (s, scope_global) ]] :=
		true.
	[[ False() ^ (s, scope_global) ]] :=
		true.
	[[ Null() ^ (s, scope_global) ]] :=
		true.
	
	// Unary and binary expressions.
	[[UnExp(_, e) ^ (s, scope_global) ]] :=
		[[ e ^ (s, scope_global) ]].
	[[BinExp(_, e1, e2) ^ (s, scope_global) ]] :=
		[[ e1 ^ (s, scope_global) ]],
		[[ e2 ^ (s, scope_global) ]].
	
	// Generic optional node arguments.
	[[ None() ^ (s, scope_global) ]] :=
		true.
	
	// Statement brackets.
	[[ Block(statements) ^ (s, scope_global) ]] :=
		Map1[[ statements ^ (s, scope_global) ]].
	
	// Function calls.
	[[ FuncCall(funcName, argExps) ^ (s, scope_global) ]] :=
		// TODO - Check against API to see whether a function exists?
		// TODO - Obtain type info from the API somehow?
		Map1[[ argExps ^ (s, scope_global) ]].
	
	// ArrayHandling nodes.
	[[ ArrayPush(arrayExp, valExp) ^ (s, scope_global) ]] :=
		[[ arrayExp ^ (s, scope_global) ]],
		[[ valExp ^ (s, scope_global) ]].
	
	[[ ArrayClone(arrayExp) ^ (s, scope_global) ]] :=
		[[ arrayExp ^ (s, scope_global) ]].
	
	[[ ArrayGet(arrayExp, indExp) ^ (s, scope_global) ]] :=
		[[ arrayExp ^ (s, scope_global) ]],
		[[ indExp ^ (s, scope_global) ]].
	
	[[ SliceStatic(val1, val2) ^ (s, scope_global) ]] :=
		true.
	
	[[ SliceDynamic(exp1, exp2) ^ (s, scope_global) ]] :=
		[[ exp1 ^ (s, scope_global) ]],
		[[ exp2 ^ (s, scope_global) ]].
	
	[[ ArraySet(arrayExp, indExp, valExp) ^ (s, scope_global) ]] :=
		[[ arrayExp ^ (s, scope_global) ]],
		[[ indExp ^ (s, scope_global) ]],
		[[ valExp ^ (s, scope_global) ]].
	
	// BasicLogic nodes.
	// None (handled through UnOp and BinOp).
	
	// ControlFlow nodes.
	[[ If(cond, ifCode, None()) ^ (s, scope_global) ]] :=
		[[ cond ^ (s, scope_global) ]],
		Map1[[ ifCode ^ (s, scope_global) ]].
	[[ If(cond, ifCode, elseCode) ^ (s, scope_global) ]] :=
		[[ cond ^ (s, scope_global) ]],
		Map1[[ ifCode ^ (s, scope_global) ]],
		Map1[[ elseCode ^ (s, scope_global) ]].
	
	[[ While(cond, code) ^ (s, scope_global) ]] :=
		[[ cond ^ (s, scope_global) ]],
		Map1[[ code ^ (s, scope_global) ]].
	
	[[ DoWhile(code, cond) ^ (s, scope_global) ]] :=
		Map1[[ code ^ (s, scope_global) ]],
		[[ cond ^ (s, scope_global) ]].
	
	[[ For(assign, cond, exp, code, None()) ^ (s, scope_global) ]] :=
		[[ assign ^ (s, scope_global) ]],
		[[ cond ^ (s, scope_global) ]],
		[[ exp ^ (s, scope_global) ]],
		Map1[[ code ^ (s, scope_global) ]].
	[[ For(assign, cond, exp, code, elseCode) ^ (s, scope_global) ]] :=
		[[ assign ^ (s, scope_global) ]],
		[[ cond ^ (s, scope_global) ]],
		[[ exp ^ (s, scope_global) ]],
		Map1[[ code ^ (s, scope_global) ]],
		Map1[[ elseCode ^ (s, scope_global) ]].
	
	[[ ForEach(key, val, arrayExp, code, None()) ^ (s, scope_global) ]] :=
		[[ key ^ (s, scope_global) ]],
		[[ val ^ (s, scope_global) ]],
		[[ arrayExp ^ (s, scope_global) ]],
		Map1[[ code ^ (s, scope_global) ]].
	[[ ForEach(key, val, arrayExp, code, elseCode) ^ (s, scope_global) ]] :=
		[[ key ^ (s, scope_global) ]],
		[[ val ^ (s, scope_global) ]],
		[[ arrayExp ^ (s, scope_global) ]],
		Map1[[ code ^ (s, scope_global) ]],
		Map1[[ elseCode ^ (s, scope_global) ]].
	
	// TODO - Implement SwitchCase.
//	[[ SwitchCase(...) ^ (s, scope_global) ]] := ...
	
	[[ Break(numLoops) ^ (s, scope_global) ]] :=
		[[ numLoops ^ (s, scope_global) ]].
	[[ Continue(numLoopsSkipped) ^ (s, scope_global) ]] :=
		[[ numLoopsSkipped ^ (s, scope_global) ]].
	[[ Return(Some(exp)) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	[[ Return(None()) ^ (s, scope_global) ]] :=
		true.
	[[ Die(Some(exp)) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	[[ Die(None()) ^ (s, scope_global) ]] :=
		true.
	
	[[ Ternary(cond, ifExp, elseExp) ^ (s, scope_global) ]] :=
		[[ cond ^ (s, scope_global) ]],
		[[ ifExp ^ (s, scope_global) ]],
		[[ elseExp ^ (s, scope_global) ]].
	
	[[ ProcCall(procName, argExps) ^ (s, scope_global) ]] :=
		// TODO - Do argument length check. Warn about mismatching arg size?
		// The MS compiler simply ignores too many arguments and assigns null to undefined args.
		Proc{procName} -> s,
		Proc{procName} |-> d | error $[Cannot find procedure: [procName]],
		Map1[[ argExps ^ (s, scope_global) ]].
	
	[[ ProcCallDynamic(procNameExp, argExps) ^ (s, scope_global) ]] :=
		[[ procNameExp ^ (s, scope_global) ]],
		Map1[[ argExps ^ (s, scope_global) ]].
	
	[[ ProcCallDynamicArray(procNameExp, argsArray) ^ (s, scope_global) ]] :=
		[[ procNameExp ^ (s, scope_global) ]],
		[[ argsArray ^ (s, scope_global) ]].
	
	// DataHandling nodes.
	[[ ProcDecl(procName, params, code) ^ (s, scope_global) ]] :=
		
		// Store proc declaration in parent scope.
		Proc{procName} <- s,
		
		// Add scope_params as child of the parent scope.
		new scope_params,
		scope_params ---> s,
		
		// Add scope_proc as child of the params scope.
		new scope_proc,
		scope_proc ---> scope_params,
		
		// Add parameters to params scope.
		Map1[[ params ^ (s, scope_params, scope_global) ]],
		
		// Resolve code in proc scope.
		Map1[[ code ^ (scope_proc, scope_global) ]].
	
	[[ Array(entries) ^ (s, scope_global) ]] :=
		Map1[[entries ^ (s, scope_global)]]. // TODO - Check for duplicate keys?
	[[ AssocArray(entries) ^ (s, scope_global) ]] :=
		Map1[[entries ^ (s, scope_global)]]. // TODO - Check for duplicate keys?
	[[ ArrayLabelEntry(_, valExp) ^ (s, scope_global) ]] :=
		[[ valExp ^ (s, scope_global) ]].
	
	[[ BoolCast(exp) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	[[ DoubleCast(exp) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	[[ IntCast(exp) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	[[ StringCast(exp) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	
	[[ Closure(params, code) ^ (s, scope_global) ]] :=
		
		// Add scope_params as child of the parent scope.
		new scope_params,
		scope_params ---> s,
		
		// Add scope_closure as child of the params scope.
		new scope_closure,
		scope_closure ---> scope_params,
		
		// Add parameters to params scope.
		IVar{"@arguments"} <- scope_params, // Automatically added.
		Map1[[ params ^ (s, scope_params, scope_global) ]],
		
		// Resolve code in closure scope.
		Map1[[ code ^ (scope_closure, scope_global) ]].
	
	[[ IClosure(params, code) ^ (s, scope_global) ]] :=
		
		// Create scope_params.
		new scope_params,
		
		// Add scope_closure as child of the params scope.
		new scope_closure,
		scope_closure ---> scope_params,
		
		// Add parameters to params scope.
		IVar{"@arguments"} <- scope_params, // Automatically added.
		Map1[[ params ^ (s, scope_params, scope_global) ]],
		
		// Resolve code in closure scope.
		Map1[[ code ^ (scope_closure, scope_global) ]].
	
	[[ RClosure(params, code) ^ (s, scope_global) ]] :=
		
		// Create scope_params.
		new scope_params,
		
		// Add scope_closure as child of the params scope.
		new scope_closure,
		scope_closure ---> scope_params,
		
		// Add parameters to params scope.
		IVar{"@arguments"} <- scope_params, // Automatically added.
		Map1[[ params ^ (s, scope_params, scope_global) ]],
		
		// Resolve code in closure scope.
		Map1[[ code ^ (scope_closure, scope_global) ]].
	
//	[[ IVarAssign(varName, valExp) ^ (s, scope_global) ]] :=
//		IVar{varName} -> s, // varName is a reference in s.
//		IVar{varName} |-> d,
//		[[valExp ^ (s, scope_global)]].
	[[ IVarAssign(varName, valExp) ^ (s, scope_global) ]] :=
		IVar{varName@s} <- s,
		[[ valExp ^ (s, scope_global) ]].
	
	// EventBinding nodes.
	[[ Cancel(None()) ^ (s, scope_global) ]] :=
		true.
	[[ Cancel(exp) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	
	[[ Consume() ^ (s, scope_global) ]] :=
		true.
	
	[[ Lock(exps) ^ (s, scope_global) ]] :=
		Map1[[ exps ^ (s, scope_global) ]].
	
	[[ IsCancelled() ^ (s, scope_global) ]] :=
		true.
	
	[[ IsConsumed() ^ (s, scope_global) ]] :=
		true.
	
	[[ IsLocked(exp) ^ (s, scope_global) ]] :=
		[[ exp ^ (s, scope_global) ]].
	
	[[ Bind(name, prio, filter, eventVarName, params, code) ^ (s, scope_global) ]] :=
		[[ name ^ (s, scope_global) ]],
		[[ prio ^ (s, scope_global) ]],
		[[ filter ^ (s, scope_global) ]],
		
		// Create scope_params.
		new scope_params,
		
		// Add scope_bind as child of the params scope.
		new scope_bind,
		scope_bind ---> scope_params,
		
		// Add event variable and parameters to params scope.
		IVar{eventVarName} <- scope_params,
		Map1[[ params ^ (s, scope_params, scope_global) ]],
		
		// Resolve code in bind scope.
		Map1[[ code ^ (scope_bind, scope_global) ]].
	
	[[ ModifyEvent(paramExp, valExp, throwOnFailExp) ^ (s, scope_global) ]] :=
		[[ paramExp ^ (s, scope_global) ]],
		[[ valExp ^ (s, scope_global) ]],
		[[ throwOnFailExp ^ (s, scope_global) ]].
	
	// Exceptions nodes.
	// TODO - Implement.
	
	// Math nodes.
	// None (handled through UnOp and BinOp).
	
	// StringHandling nodes.
	// None (handled through BinOp).

signature
	name resolution
		namespaces Var IVar Proc