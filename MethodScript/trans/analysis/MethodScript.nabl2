module analysis/MethodScript

imports
	signatures/-

rules
	
	// Scope initialization.
	init ^ (s) :=
		new s.
	
	// MethodScript program.
	[[ Empty() ^ (s) ]] :=
		true.
	[[ Ms(statements) ^ (s) ]] :=
		distinct/name D(s)/Proc | error $[Duplicate procedure declaration: [NAME]] @NAMES,
		Map1[[ statements ^ (s) ]].
	
	// Variable references.
	[[ IVar(varName) ^ (s) ]] :=
		IVar{varName} -> s, // varName is a reference in s.
		IVar{varName} |-> d | error $[Cannot find variable: [varName]].
	[[ Var(varName) ^ (s) ]] :=
		Var{varName} -> s, // varName is a reference in s.
		Var{varName} |-> d | error $[Cannot find variable: [varName]].
	
	// IVariable parameters.
	// With 1 scope, the param is declared and resolved in the same scope.
	// With 2 scopes, the param is declared in the second scope and its value resolved in the first.
	[[ IVarParam(varName) ^ (s) ]] :=
		IVar{varName@s} <- s.
	[[ IVarAssignParam(varName, valExp) ^ (s) ]] :=
		IVar{varName@s} <- s,
		[[ valExp ^ (s) ]].
	[[ IVarScopeParam(varName) ^ (scope_parent, scope_params) ]] :=
		IVar{varName} <- scope_params.
	[[ IVarAssignScopeParam(varName, valExp) ^ (scope_parent, scope_params) ]] :=
		IVar{varName} <- scope_params,
		[[ valExp ^ (scope_parent) ]].
	
	// Static values.
	[[ IntVal(_) ^ (s) ]] :=
		true.
	[[ FloatVal(_) ^ (s) ]] :=
		true.
	[[ StringVal(_) ^ (s) ]] :=
		true.
	[[ SmartStringVal(_) ^ (s) ]] :=
		true.
	[[ True() ^ (s) ]] :=
		true.
	[[ False() ^ (s) ]] :=
		true.
	[[ Null() ^ (s) ]] :=
		true.
	
	// Unary and binary expressions.
	[[UnExp(_, e) ^ (s) ]] :=
		[[ e ^ (s) ]].
	[[BinExp(_, e1, e2) ^ (s) ]] :=
		[[ e1 ^ (s) ]],
		[[ e2 ^ (s) ]].
	
	// Generic optional node arguments.
	[[ None() ^ (s) ]] :=
		true.
	
	// Statement brackets.
	[[ Block(statements) ^ (s) ]] :=
		Map1[[ statements ^ (s) ]].
	
	// Function calls.
	[[ FuncCall(funcName, argExps) ^ (s) ]] :=
		// TODO - Check against API to see whether a function exists?
		// TODO - Obtain type info from the API somehow?
		Map1[[ argExps ^ (s) ]].
	
	// ArrayHandling nodes.
	[[ ArrayPush(arrayExp, valExp) ^ (s) ]] :=
		[[ arrayExp ^ (s) ]],
		[[ valExp ^ (s) ]].
	
	[[ ArrayClone(arrayExp) ^ (s) ]] :=
		[[ arrayExp ^ (s) ]].
	
	[[ ArrayGet(arrayExp, indExp) ^ (s) ]] :=
		[[ arrayExp ^ (s) ]],
		[[ indExp ^ (s) ]].
	
	[[ SliceStatic(val1, val2) ^ (s) ]] :=
		true.
	
	[[ SliceDynamic(exp1, exp2) ^ (s) ]] :=
		[[ exp1 ^ (s) ]],
		[[ exp2 ^ (s) ]].
	
	[[ ArraySet(arrayExp, indExp, valExp) ^ (s) ]] :=
		[[ arrayExp ^ (s) ]],
		[[ indExp ^ (s) ]],
		[[ valExp ^ (s) ]].
	
	// BasicLogic nodes.
	// None (handled through UnOp and BinOp).
	
	// ControlFlow nodes.
	[[ If(cond, ifCode, None()) ^ (s) ]] :=
		[[ cond ^ (s) ]],
		Map1[[ ifCode ^ (s) ]].
	[[ If(cond, ifCode, elseCode) ^ (s) ]] :=
		[[ cond ^ (s) ]],
		Map1[[ ifCode ^ (s) ]],
		Map1[[ elseCode ^ (s) ]].
	
	[[ While(cond, code) ^ (s) ]] :=
		[[ cond ^ (s) ]],
		Map1[[ code ^ (s) ]].
	
	[[ DoWhile(code, cond) ^ (s) ]] :=
		Map1[[ code ^ (s) ]],
		[[ cond ^ (s) ]].
	
	[[ For(assign, cond, exp, code, None()) ^ (s) ]] :=
		[[ assign ^ (s) ]],
		[[ cond ^ (s) ]],
		[[ exp ^ (s) ]],
		Map1[[ code ^ (s) ]].
	[[ For(assign, cond, exp, code, elseCode) ^ (s) ]] :=
		[[ assign ^ (s) ]],
		[[ cond ^ (s) ]],
		[[ exp ^ (s) ]],
		Map1[[ code ^ (s) ]],
		Map1[[ elseCode ^ (s) ]].
	
	[[ ForEach(key, val, arrayExp, code, None()) ^ (s) ]] :=
		[[ key ^ (s) ]],
		[[ val ^ (s) ]],
		[[ arrayExp ^ (s) ]],
		Map1[[ code ^ (s) ]].
	[[ ForEach(key, val, arrayExp, code, elseCode) ^ (s) ]] :=
		[[ key ^ (s) ]],
		[[ val ^ (s) ]],
		[[ arrayExp ^ (s) ]],
		Map1[[ code ^ (s) ]],
		Map1[[ elseCode ^ (s) ]].
	
	// TODO - Implement SwitchCase.
//	[[ SwitchCase(...) ^ (s) ]] := ...
	
	[[ Break(numLoops) ^ (s) ]] :=
		[[ numLoops ^ (s) ]].
	[[ Continue(numLoopsSkipped) ^ (s) ]] :=
		[[ numLoopsSkipped ^ (s) ]].
	[[ Return(Some(exp)) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	[[ Return(None()) ^ (s) ]] :=
		true.
	[[ Die(Some(exp)) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	[[ Die(None()) ^ (s) ]] :=
		true.
	
	[[ Ternary(cond, ifExp, elseExp) ^ (s) ]] :=
		[[ cond ^ (s) ]],
		[[ ifExp ^ (s) ]],
		[[ elseExp ^ (s) ]].
	
	[[ ProcCall(procName, argExps) ^ (s) ]] :=
		// TODO - Do argument length check. Warn about mismatching arg size?
		// The MS compiler simply ignores too many arguments and assigns null to undefined args.
		Proc{procName} -> s,
		Proc{procName} |-> d | error $[Cannot find procedure: [procName]],
		Map1[[ argExps ^ (s) ]].
	
	[[ ProcCallDynamic(procNameExp, argExps) ^ (s) ]] :=
		[[ procNameExp ^ (s) ]],
		Map1[[ argExps ^ (s) ]].
	
	[[ ProcCallDynamicArray(procNameExp, argsArray) ^ (s) ]] :=
		[[ procNameExp ^ (s) ]],
		[[ argsArray ^ (s) ]].
	
	// DataHandling nodes.
	[[ ProcDecl(procName, params, code) ^ (s) ]] :=
		
		// Store proc declaration in parent scope.
		Proc{procName} <- s,
		
		// Add scope_params as child of the parent scope.
		new scope_params,
		scope_params ---> s,
		
		// Add scope_proc as child of the params scope.
		new scope_proc,
		scope_proc ---> scope_params,
		
		// Add parameters to params scope.
		Map1[[ params ^ (s, scope_params) ]],
		
		// Resolve code in proc scope.
		Map1[[ code ^ (scope_proc) ]].
	
	[[ Array(entries) ^ (s) ]] :=
		Map1[[entries ^ (s)]]. // TODO - Check for duplicate keys?
	[[ AssocArray(entries) ^ (s) ]] :=
		Map1[[entries ^ (s)]]. // TODO - Check for duplicate keys?
	[[ ArrayLabelEntry(_, valExp) ^ (s) ]] :=
		[[ valExp ^ (s) ]].
	
	[[ BoolCast(exp) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	[[ DoubleCast(exp) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	[[ IntCast(exp) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	[[ StringCast(exp) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	
	[[ Closure(params, code) ^ (s) ]] :=
		
		// Add scope_params as child of the parent scope.
		new scope_params,
		scope_params ---> s,
		
		// Add scope_closure as child of the params scope.
		new scope_closure,
		scope_closure ---> scope_params,
		
		// Add parameters to params scope.
		IVar{"@arguments"} <- scope_params, // Automatically added.
		Map1[[ params ^ (s, scope_params) ]],
		
		// Resolve code in closure scope.
		Map1[[ code ^ (scope_closure) ]].
	
	[[ IClosure(params, code) ^ (s) ]] :=
		
		// Create scope_params.
		new scope_params,
		
		// Add scope_closure as child of the params scope.
		new scope_closure,
		scope_closure ---> scope_params,
		
		// Add parameters to params scope.
		IVar{"@arguments"} <- scope_params, // Automatically added.
		Map1[[ params ^ (s, scope_params) ]],
		
		// Resolve code in closure scope.
		Map1[[ code ^ (scope_closure) ]].
	
	[[ RClosure(params, code) ^ (s) ]] :=
		
		// Create scope_params.
		new scope_params,
		
		// Add scope_closure as child of the params scope.
		new scope_closure,
		scope_closure ---> scope_params,
		
		// Add parameters to params scope.
		IVar{"@arguments"} <- scope_params, // Automatically added.
		Map1[[ params ^ (s, scope_params) ]],
		
		// Resolve code in closure scope.
		Map1[[ code ^ (scope_closure) ]].
	
//	[[ IVarAssign(varName, valExp) ^ (s) ]] :=
//		IVar{varName} -> s, // varName is a reference in s.
//		IVar{varName} |-> d,
//		[[valExp ^ (s)]].
	[[ IVarAssign(varName, valExp) ^ (s) ]] :=
		IVar{varName@s} <- s,
		[[ valExp ^ (s) ]].
	
	// EventBinding nodes.
	[[ Cancel(None()) ^ (s) ]] :=
		true.
	[[ Cancel(exp) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	
	[[ Consume() ^ (s) ]] :=
		true.
	
	[[ Lock(exps) ^ (s) ]] :=
		Map1[[ exps ^ (s) ]].
	
	[[ IsCancelled() ^ (s) ]] :=
		true.
	
	[[ IsConsumed() ^ (s) ]] :=
		true.
	
	[[ IsLocked(exp) ^ (s) ]] :=
		[[ exp ^ (s) ]].
	
	[[ Bind(name, prio, filter, eventVarName, params, code) ^ (s) ]] :=
		[[ name ^ (s) ]],
		[[ prio ^ (s) ]],
		[[ filter ^ (s) ]],
		
		// Create scope_params.
		new scope_params,
		
		// Add scope_bind as child of the params scope.
		new scope_bind,
		scope_bind ---> scope_params,
		
		// Add event variable and parameters to params scope.
		IVar{eventVarName} <- scope_params,
		Map1[[ params ^ (s, scope_params) ]],
		
		// Resolve code in bind scope.
		Map1[[ code ^ (scope_bind) ]].
	
	[[ ModifyEvent(paramExp, valExp, throwOnFailExp) ^ (s) ]] :=
		[[ paramExp ^ (s) ]],
		[[ valExp ^ (s) ]],
		[[ throwOnFailExp ^ (s) ]].
	
	// Exceptions nodes.
	// TODO - Implement.
	
	// Math nodes.
	// None (handled through UnOp and BinOp).
	
	// StringHandling nodes.
	// None (handled through BinOp).

signature
	name resolution
		namespaces Var IVar Proc