module MethodScript

imports
	Common
	ArrayHandling
	BasicLogic
	ControlFlow
	DataHandling
	EventBinding
	Exceptions
	Math
	StringHandling

context-free start-symbols
	Start Statement Exp

context-free syntax
	Start.Empty = <>
	Start.Program = Program
	
	Program.Msa = CmdDecl+
	Program.Ms = Statement+
	
	/*
	 * Statement: Single coherent code that does not result in a value. Cannot be used as Exp.
	 * SExp: Statement that resolves to some value. Can be used as Statement and as Exp.
	 * Exp: Expression that resolves to some value. Cannot be used as Statement.
	 */
	Statement.SExp = <<SExp><SemiColon?>
	>
	Exp.SExp = SExp
	SemiColon.SemiColon = ";"
	
	/*
	 * Define an empty term that can be added to ambiguous terms where the root of the ambiguity occurs in a
	 * parent term, allowing disambiguation using {prefer} or {avoid}.
	 * Note that this only works if the parent terms have equal children, except for the ambiguous child term.
	 */
	 EmptyTerm = ""
	
	/* 
	 * Allow extended statements in Statement+ and Statement*.
	 * These are statements that might cause an ambiguity with the next or previous statement,
	 * in which case the extended statement will be avoided.
	 */
	Statement+ = Statement+ StatementEx EmptyTerm {avoid}
	Statement+ = StatementEx {avoid}
	
	// Statements.
	
	// Expressions.
	
	// Variables.
	Exp.IVar = IVAR
	Exp.Var = VAR
	
	// Parameters that use a single scope for declaring and resolving.
	IVarParam.IVarParam = <<CLASSTYPE-ID?><IVAR>>
	IVarParam.IVarAssignParam = <<CLASSTYPE-ID?><IVAR> = <Exp>>
	IVarParam.IVarAssignParamLegacy = <assign(<IVarCTypeIdLegacy?><IVAR>, <Exp>)>
	IVarCTypeIdLegacy.IVarCTypeIdLegacy = <<CLASSTYPE-ID>, >
	
	// Parameters that use a different scope for declaring and resolving.
	IVarScopeParam.IVarScopeParam = <<CLASSTYPE-ID?><IVAR>>
	IVarScopeParam.IVarAssignScopeParam = <<CLASSTYPE-ID?><IVAR> = <Exp>>
	IVarScopeParam.IVarAssignScopeParamLegacy = <assign(<IVarCTypeIdLegacy?><IVAR>, <Exp>)>
	
	// Static values.
	Exp.IntVal = INT
	Exp.DoubleVal = DOUBLE
	Exp.StringVal = STRING
	Exp.SmartStringVal = SMART-STRING
	Exp.ClassTypeVal = CLASSTYPE-ID
	Exp.True = "true"
	Exp.False = "false"
	Exp.Null = "null"
	
	// Function calls. Functions that accept code blocks as an argument should be defined separately.
	SExp.FuncCall = <<FUNC-ID>(<{Exp ","}*>)>
	
	// Command declarations.
	CmdDecl.CmdDecl = <*:/<CMD-ID+><CmdArgDecl*><CmdOptArgDecl*> =<CmdBody>>
	
	CmdArgDecl.CmdArgDecl = < <VAR>>
	CmdOptArgDecl.CmdOptArgDecl = < [<VAR>]>
	CmdOptArgDecl.CmdOptArgDeclDefault = < [<VAR>=<Exp>]>
	
	CmdBody.SingleLine = < <Statement>>
	CmdBody.MultiLine = [ >>>
		[Statement*]
	<<<]
	
	/* Brackets.
	 * These are allowed around expressions and statements. However, when put around statements which
	 * have a statement before them, odds are high that it is lexed as a function in the official
	 * MethodScript compiler. So don't support brackets around statements.
	 */
	Exp = <(<Exp>)> {bracket}

context-free priorities
	// Operator priorities (tested): {array_clone, array_get} > {neg, not} > {**} > {*, /, %} > {+, -, .}
	// > {>, <, >=, <=} > {==, !=, ===, !==} > {&&&} > {|||} > {&&} > {||} > {assign, array_set}.
	// Unary operator priorities (tested): {neg} > {postinc, postdec} > {inc, dec} > {not}.
	// Pos/Neg VS PostInc/PostDec priorities are swapped in the implementation for disambiguation of
	// "@a = 1 + (func()++)" VS "@a = 1 (+ func()++)".
	{Exp.ArrayClone Exp.ArrayGet}
			> {SExp.PostInc SExp.PostDec}
			> {Exp.Neg Exp.Pos}
			> {Exp.Inc StatementEx.Inc Exp.Dec StatementEx.Dec}
			> Exp.Not
			> Exp.Pow
			> {left: Exp.Multiply Exp.Divide Exp.Mod}
			> {left: Exp.Add Exp.Subtract Exp.Concat}
			> {non-assoc: Exp.Gt Exp.Lt Exp.Gte Exp.Lte}
			> {Exp.Equals Exp.NotEquals Exp.Sequals Exp.NotSequals}
			> Exp.Dand > Exp.Dor > Exp.And > Exp.Or
			> {SExp.IVarAssign SExp.ArraySet SExp.IVarAddAssign SExp.IVarSubtractAssign
					SExp.IVarMultiplyAssign SExp.IVarDivideAssign SExp.IVarConcatAssign}
