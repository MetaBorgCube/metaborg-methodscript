module Common

lexical syntax
	ID             = [a-zA-Z] [a-zA-Z0-9\_]*
	INT            = [0-9]+
	SIGNED-INT     = "-"? [0-9]+
	FLOAT          = [0-9]+ "." [0-9]+ // TODO - This should probably accept everything that Double.parseDouble() accepts.
	VAR            = "$" [a-zA-Z0-9\_]+
	IVAR           = "@" [a-zA-Z0-9\_]+
	PROC-ID        = "_" [a-zA-Z0-9] [a-zA-Z0-9\_]*
	FUNC-ID        = [a-zA-Z0-9] [a-zA-Z0-9\_]* // TODO - Do internal functions exist that start with a '_'?
	LABEL          = STRING
	LABEL          = SMART-STRING // TODO - Support this? SS's with a variable in them eval to 'smart_string' in labels.
	LABEL          = BARE-STRING
	STRING         = "'" StringChar* "'"
	SMART-STRING   = "\"" SmartStringChar* "\"" // TODO - Implement smart string features.
	BARE-STRING    = [a-zA-Z0-9\_]+ // TODO - Bare strings can also contain §, ±, etc. But not any operator/brace/bracket.
	StringChar     = ~[\']
	StringChar     = "\\'"
	StringChar     = BackSlashChar // TODO - Check if necessary.
	SmartStringChar= ~[\"]
	SmartStringChar= "\\\""
	SmartStringChar= BackSlashChar // TODO - Check if necessary.
	BackSlashChar  = "\\"
	LAYOUT         = [\ \t\n\r]
	CommentChar    = [\*]
	LAYOUT         = "/*" InsideComment* "*/"
	InsideComment  = ~[\*]
	InsideComment  = CommentChar
	LAYOUT         = "//" ~[\n\r]* NewLineEOF
	LAYOUT         = "#" ~[\n\r]* NewLineEOF
	NewLineEOF     = [\n\r]
	NewLineEOF     = EOF
	EOF            = ""
	
	// Exclude function IDs that have custom handling.
	FUNC-ID = "array" {reject}
	FUNC-ID = "associative_array" {reject}
	FUNC-ID = "break" {reject}
	FUNC-ID = "continue" {reject}
	FUNC-ID = "return" {reject}
	FUNC-ID = "if" {reject}
	FUNC-ID = "while" {reject}
	FUNC-ID = "dowhile" {reject}
	FUNC-ID = "for" {reject}
	FUNC-ID = "forelse" {reject}
	FUNC-ID = "bind" {reject}
	FUNC-ID = "die" {reject}
	FUNC-ID = "proc" {reject}
	FUNC-ID = "closure" {reject}
	FUNC-ID = "iclosure" {reject}
	FUNC-ID = "rclosure" {reject}
	
	// Exclude cast functions.
	FUNC-ID = "boolean" {reject}
	FUNC-ID = "double" {reject}
	FUNC-ID = "integer" {reject}
	FUNC-ID = "string" {reject}
	
//	// Exclude keywords.
//	BARE-STRING = "true" {reject}
//	BARE-STRING = "false" {reject}

lexical restrictions
	
	// Ensure greedy matching for lexicals.
	CommentChar   -/- [\/]
	INT           -/- [0-9\.]
	SIGNED-INT    -/- [0-9]
	FLOAT         -/- [0-9\.]
	ID            -/- [a-zA-Z0-9\_]
	VAR           -/- [a-zA-Z0-9\_]
	IVAR          -/- [a-zA-Z0-9\_]
	PROC-ID       -/- [a-zA-Z0-9\_]
	FUNC-ID       -/- [a-zA-Z0-9\_]
	BARE-STRING   -/- [a-zA-Z0-9\_\(]
	
	// EOF may not be followed by any char.
	EOF           -/- ~[]
	
	// Backslash chars in strings may not be followed by ".
	BackSlashChar -/- [\"]

context-free restrictions
	
	// Ensure greedy matching for comments.
	LAYOUT? -/- [\ \t\n\r]
	LAYOUT? -/- [\/].[\/]
	LAYOUT? -/- [\#]
	LAYOUT? -/- [\/].[\*]
